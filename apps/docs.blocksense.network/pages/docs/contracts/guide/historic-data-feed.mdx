import { Callout, Tabs } from 'nextra/components';

# Historic Data Feed Store

Historic Data Feed Store contract is where all data is stored and where all contract read from. It is written in Solidity assembly and contains only one function: `fallback`.

## Code Examples

### Solidity

<Callout type="info">
  Use [ProxyCall
  library](https://github.com/blocksense-network/blocksense/tree/main/libs/contracts/contracts/libraries/ProxyCall.sol)
  for easy and gas optimised interaction with the contract.
</Callout>

In order to retrieve data from the contract, the client must call the `fallback` function as follows:

- To read the latest price and timestamp:

<Tabs items={['ProxyCall', 'Raw call']}>
    <Tabs.Tab>
      ```solidity copy filename="solidity"
      function getDataById(
        uint32 key
      ) external view returns (uint256 value, uint64 timestamp) {
        bytes32 data = ProxyCall._callDataFeed(
          dataFeedStore,
          abi.encodePacked(0x80000000 | key)
        );

        return (uint256(uint192(bytes24(data))), uint64(uint256(data)));
      }
      ```
    </Tabs.Tab>

    <Tabs.Tab>
      ```solidity copy filename="solidity"
      function getDataById(
        uint32 key
      ) external view returns (uint256 value, uint64 timestamp) {
        (bool success, bytes memory returnData) = dataFeedStore.staticcall(
          abi.encodePacked(0x80000000 | key)
        );
        require(success, "DataFeedStore: call failed");

        bytes32 data = bytes32(returnData);
        return (uint256(uint192(bytes24(data))), uint64(uint256(data)));
      }
      ```
    </Tabs.Tab>

</Tabs>

- To read historical data for a specific round:

<Tabs items={['ProxyCall', 'Raw call']}>
    <Tabs.Tab>
      ```solidity copy filename="solidity"
      function getFeedAtCounter(
        uint32 key,
        uint32 counter
      ) external view returns (uint256 value, uint64 timestamp) {
        bytes32 data = ProxyCall._callDataFeed(
          dataFeedStore,
          abi.encodeWithSelector(bytes4(0x20000000 | key), counter)
        );

        return (uint256(uint192(bytes24(data))), uint64(uint256(data)));
      }
      ```
    </Tabs.Tab>

    <Tabs.Tab>
      ```solidity copy filename="solidity"
      function getFeedAtCounter(
        uint32 key,
        uint32 counter
      ) external view returns (uint256 value, uint64 timestamp) {
        (bool success, bytes memory returnData) = dataFeedStore.staticcall(
          abi.encodeWithSelector(bytes4(0x20000000 | key), counter)
        );
        require(success, "DataFeedStore: call failed");

        bytes32 data = bytes32(returnData);
        return (uint256(uint192(bytes24(data))), uint64(uint256(data)));
      }
      ```
    </Tabs.Tab>

</Tabs>

- To get the latest round:

<Tabs items={['ProxyCall', 'Raw call']}>
    <Tabs.Tab>
      ```solidity copy filename="solidity"
      function getLatestCounter(uint32 key) external view returns (uint32 counter) {
        return uint32(ProxyCall._latestRound(key, dataFeedStore));
      }
      ```
    </Tabs.Tab>

    <Tabs.Tab>
      ```solidity copy filename="solidity"
      function getLatestCounter(uint32 key) external view returns (uint32 counter) {
        (bool success, bytes memory returnData) = dataFeedStore.staticcall(
          abi.encodePacked(0x40000000 | key)
        );
        require(success, "DataFeedStore: call failed");

        return uint32(uint256(bytes32(returnData)));
      }
      ```
    </Tabs.Tab>

</Tabs>

- To get the latest round data:

<Tabs items={['ProxyCall', 'Raw call']}>
    <Tabs.Tab>
      ```solidity copy filename="solidity"
      function getDataById(
        uint32 key
      ) external view returns (int256 value, uint256 timestamp, uint80 counter) {
        (counter, value, timestamp, , ) = ProxyCall
          ._latestRoundData(key, dataFeedStore);
      }
      ```
    </Tabs.Tab>

    <Tabs.Tab>
      ```solidity copy filename="solidity"
      function getDataById(
        uint32 key
      ) external view returns (uint256 value, uint64 timestamp, uint256 counter) {
        (bool success, bytes memory returnData) = dataFeedStore.staticcall(
          abi.encodePacked(0xc0000000 | key)
        );
        require(success, "DataFeedStore: call failed");

        value = uint256(uint192(bytes24(returnData)));
        timestamp = uint64(uint256(bytes32(returnData)));
        assembly {
          returnData := add(returnData, 0x20)
        }
        counter = uint256(bytes32(returnData));
      }
      ```
    </Tabs.Tab>

</Tabs>

### Solidity Hardhat Example

<Callout type="info" title="Note">
  You can find a working Hardhat project
  [here](https://github.com/blocksense-network/blocksense/tree/main/libs/contracts).
  Clone the repo and follow the setup instructions to run the example locally.
</Callout>

### Ethers.js v6.x

To read the latest value and timestamp:

```js copy filename="javascript"
const historicDataFeedStore = new ethers.Contract(
  contractAddress,
  abiJson,
  provider,
);
const data = '0x' + ((key | 0x80000000) >>> 0).toString(16).padStart(8, '0');

const res = await network.provider.send('eth_call', [
  {
    to: historicDataFeedStore.target,
    data,
  },
  'latest',
]);

const value = Number(res.slice(0, 50));
const timestamp = Number('0x' + res.slice(50, 66));
```

To read historical data for a specific round:

```js copy filename="javascript"
const historicDataFeedStore = new ethers.Contract(
  contractAddress,
  abiJson,
  provider,
);
const round = 1;
const data = '0x' + ((key | 0x20000000) >>> 0).toString(16).padStart(8, '0');

const res = await network.provider.send('eth_call', [
  {
    to: historicDataFeedStore.target,
    data: ethers.solidityPacked(['bytes4', 'uint256'], [data, round]),
  },
  'latest',
]);

const value = Number(res.slice(0, 50));
const timestamp = Number('0x' + res.slice(50, 66));
```

To get the latest round:

```js copy filename="javascript"
const historicDataFeedStore = new ethers.Contract(
  contractAddress,
  abiJson,
  provider,
);
const data = '0x' + ((key | 0x40000000) >>> 0).toString(16).padStart(8, '0');

const res = await network.provider.send('eth_call', [
  {
    to: historicDataFeedStore.target,
    data,
  },
  'latest',
]);
const round = Number('0x' + res.slice(66));
```

To get the latest round data:

```js copy filename="javascript"
const historicDataFeedStore = new ethers.Contract(
  contractAddress,
  abiJson,
  provider,
);
const data = '0x' + ((key | 0xc0000000) >>> 0).toString(16).padStart(8, '0');

const res = await network.provider.send('eth_call', [
  {
    to: historicDataFeedStore.target,
    data,
  },
  'latest',
]);

const value = Number(res.slice(0, 50));
const timestamp = Number('0x' + res.slice(50, 66));
const round = Number('0x' + res.slice(66));
```
