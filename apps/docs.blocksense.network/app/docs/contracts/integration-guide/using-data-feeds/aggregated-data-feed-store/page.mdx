---
title: Aggregated Data Feed Store
---

import { Callout } from "@blocksense/docs-ui/Callout";
import { Tabs, TabsContent, TabsList, TabsTrigger } from '@blocksense/docs-ui/Tabs';

# Aggregated Data Feed Store (ADFS)

Aggregated Data Feed Store contract is where all data is stored and where all contracts read from. It is written in Solidity assembly and contains only one function: fallback.

<Callout type="info" emoji="âš ï¸">
  This contract serves as the core logic that dictates how the [UpgradeableProxyADFS](../../reference-documentation/contract/UpgradeableProxyADFS.mdx) contract
  manages its own storage, handling both data writing and reading operations. Understanding
  that all data is exclusively stored within the **UpgradeableProxyADFS** contract is crucial.
  To access this data, users must interact with the `fallback` function of the **UpgradeableProxyADFS**
  contract which underneath calls the `fallback` function of the **AggregatedDataFeedStore** contract.
</Callout>

The examples below show how to call the contract to fetch data. We have the following types of calls:
- selector **0x86**: getDataAtIndex(uint128 feedId, uint16 index, uint32 startSlot?, uint32 slots?) returns (bytes)
- selector **0x81**: getLatestIndex(uint128 feedId) returns (uint16)
- selector **0x82**: getLatestSingleData(uint128 feedId) returns (bytes)
- selector **0x84**: getLatestData(uint128 feedId, uint32 startSlot?, uint32 slots?) returns (bytes)

<Callout type="info" emoji="â„¹ï¸">
  - Selector **0x83** will call both getLatestIndex and getLatestSingleData.
  - Selector **0x85** will call both getLatestIndex and getLatestData.
</Callout>

The Aggregated Data Feed Store (ADFS) maintains historical data for feeds of varying sizes. Each feed's size is determined by its slot count, where 1 slot equals 32 bytes. Supported sizes follow powers of 2, ranging from 0 to 31: [2^0; 2^31] (1 slot, 2 slots, 4 slots, 8 slots, 16 slots, 32 slots, etc.).

Data is stored using a ring buffer architecture with a maximum capacity of 8,192 indices (2^13). When the buffer reaches capacity, new data overwrites the oldest entries beginning at index 0, ensuring continuous operation while maintaining the most recent historical data.

## Code Examples

### Solidity

<Callout type="info" emoji="ðŸ’¡">
  Use the [ADFS library](https://github.com/blocksense-network/blocksense/blob/main/libs/ts/contracts/contracts/libraries/ADFS.sol) for easy and gas optimised interaction with the contract.
</Callout>

Here is a complete working example. After that, there are more details on how to perform a raw call.

```solidity showLineNumbers copy filename="ADFSConsumer.sol"
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.28;

import {ADFS} from `libraries/ADFS.sol';

/**
 * THIS IS AN EXAMPLE CONTRACT THAT USES UN-AUDITED CODE.
 * DO NOT USE THIS CODE IN PRODUCTION.
 */
contract ADFSConsumer {
  address public immutable adfs;

  constructor(address _adfs) {
    adfs = _adfs;
  }

  function getLatestSingleData(uint256 id) external view returns (bytes32) {
    return ADFS.getLatestSingleData(adfs, id);
  }

  function getLatestData(uint256 id) external view returns (bytes32[] memory) {
    return ADFS.getLatestData(adfs, id);
  }

  function getLatestDataSlice(
    uint256 id,
    uint256 startSlot,
    uint256 slotsCount
  ) external view returns (bytes32[] memory) {
    return ADFS.getLatestDataSlice(adfs, id, startSlot, slotsCount);
  }

  function getLatestIndex(uint256 id) external view returns (uint256 index) {
    return ADFS.getLatestIndex(adfs, id);
  }

  function getSingleDataAtIndex(
    uint256 id,
    uint256 index
  ) external view returns (bytes32) {
    return ADFS.getSingleDataAtIndex(adfs, id, index);
  }

  function getDataAtIndex(
    uint256 id,
    uint256 index
  ) external view returns (bytes32[] memory) {
    return ADFS.getDataAtIndex(adfs, id, index);
  }

  function getDataSliceAtIndex(
    uint256 id,
    uint256 index,
    uint256 startSlot,
    uint256 slotsCount
  ) external view returns (bytes32[] memory) {
    return ADFS.getDataSliceAtIndex(adfs, id, index, startSlot, slotsCount);
  }

  function getLatestSingleDataAndIndex(
    uint256 id
  ) external view returns (bytes32 data, uint256 index) {
    return ADFS.getLatestSingleDataAndIndex(adfs, id);
  }

  function getLatestDataAndIndex(
    uint256 id
  ) external view returns (bytes32[] memory data, uint256 index) {
    return ADFS.getLatestDataAndIndex(adfs, id);
  }

  function getLatestDataSliceAndIndex(
    uint256 id,
    uint256 startSlot,
    uint256 slotsCount
  ) external view returns (bytes32[] memory data, uint256 index) {
    return ADFS.getLatestDataSliceAndIndex(adfs, id, startSlot, slotsCount);
  }

  function getEpochSeconds(uint256 id) external view returns (uint64) {
    return ADFS.getEpochSeconds(ADFS.getLatestSingleData(adfs, id));
  }

  function getEpochMilliseconds(uint256 id) external view returns (uint64) {
    return ADFS.getEpochMilliseconds(ADFS.getLatestSingleData(adfs, id));
  }
}
```

In order to retrieve data from the contract, the client must call the `fallback` function as follows:

- To read the latest data stored in a feed stored in up to 32 bytes:

<Tabs defaultValue="lib-call" className="mt-4">
  <TabsList>
    <TabsTrigger value="lib-call">Lib Call</TabsTrigger>
    <TabsTrigger value="raw-call">Raw Call</TabsTrigger>
  </TabsList>
    <TabsContent value="lib-call">
      ```solidity copy filename="solidity"
      function getLatestSingleData(uint256 id) external view returns (bytes32) {
        return ADFS.getLatestSingleData(adfs, id);
      }
      ```
    </TabsContent>

      <TabsContent value="raw-call">
      ```solidity copy filename="solidity"
      function getLatestSingleData(
        uint256 id
      ) external view returns (bytes32 data) {
        (bool success, bytes memory returnData) = adfs.staticcall(
          abi.encodePacked(bytes1(0x82), uint128(id))
        );
        require(success, 'ADFS: call failed');

        return (bytes32(returnData));
      }
      ```
    </TabsContent>

</Tabs>

- To read the latest data stored in a feed stored in more than 32 bytes:

<Tabs defaultValue="lib-call" className="mt-4">
  <TabsList>
    <TabsTrigger value="lib-call">Lib Call</TabsTrigger>
    <TabsTrigger value="raw-call">Raw Call</TabsTrigger>
  </TabsList>
    <TabsContent value="lib-call">
      ```solidity copy filename="solidity"
      function getLatestData(uint256 id) external view returns (bytes32[] memory) {
        return ADFS.getLatestData(adfs, id);
      }
      ```
    </TabsContent>

      <TabsContent value="raw-call">
      ```solidity copy filename="solidity"
      function getLatestData(
        uint256 id
      ) external view returns (bytes32[] memory data) {
        (bool success, bytes memory returnData) = adfs.staticcall(
          abi.encodePacked(bytes1(0x84), uint128(id))
        );
        require(success, 'ADFS: call failed');

        return parseBytesToBytes32Array(returnData);
      }
      ```
    </TabsContent>

</Tabs>

- To read a slice of the latest data stored in a feed stored in more than 32 bytes:

<Tabs defaultValue="lib-call" className="mt-4">
  <TabsList>
    <TabsTrigger value="lib-call">Lib Call</TabsTrigger>
    <TabsTrigger value="raw-call">Raw Call</TabsTrigger>
  </TabsList>
    <TabsContent value="lib-call">
      ```solidity copy filename="solidity"
      function getLatestDataSlice(
        uint256 id,
        uint256 startSlot,
        uint256 slotsCount
      ) external view returns (bytes32[] memory) {
        return ADFS.getLatestDataSlice(adfs, id, startSlot, slotsCount);
      }
      ```
    </TabsContent>

      <TabsContent value="raw-call">
      ```solidity copy filename="solidity"
      function getLatestDataSlice(
        uint256 id,
        uint256 startSlot,
        uint256 slotsCount
      ) external view returns (bytes32[] memory) {
        return ADFS.getLatestDataSlice(adfs, id, startSlot, slotsCount);
      }
      ```
    </TabsContent>

</Tabs>

- To get the latest ring buffer index:

<Tabs defaultValue="lib-call" className="mt-4">
  <TabsList>
    <TabsTrigger value="lib-call">Lib Call</TabsTrigger>
    <TabsTrigger value="raw-call">Raw Call</TabsTrigger>
  </TabsList>
    <TabsContent value="lib-call">
      ```solidity copy filename="solidity"
      function getLatestIndex(uint256 id) external view returns (uint256 index) {
        return ADFS.getLatestIndex(adfs, id);
      }
      ```
    </TabsContent>

      <TabsContent value="raw-call">
      ```solidity copy filename="solidity"
      function getLatestIndex(uint256 id) external view returns (uint256 index) {
        (bool success, bytes memory returnData) = adfs.staticcall(
          abi.encodePacked(bytes1(0x81), uint128(id))
        );
        require(success, 'ADFS: call failed');

        return uint256(bytes32(returnData));
      }
      ```
    </TabsContent>

</Tabs>

- To read historical data stored in a feed stored in up to 32 bytes:

<Tabs defaultValue="lib-call" className="mt-4">
  <TabsList>
    <TabsTrigger value="lib-call">Lib Call</TabsTrigger>
    <TabsTrigger value="raw-call">Raw Call</TabsTrigger>
  </TabsList>
    <TabsContent value="lib-call">
      ```solidity copy filename="solidity"
      function getSingleDataAtIndex(
        uint256 id,
        uint256 index
      ) external view returns (bytes32) {
        return ADFS.getSingleDataAtIndex(adfs, id, index);
      }
      ```
    </TabsContent>

      <TabsContent value="raw-call">
      ```solidity copy filename="solidity"
      function getSingleDataAtIndex(
        uint256 id,
        uint256 index
      ) external view returns (bytes32 data) {
        (bool success, bytes memory returnData) = adfs.staticcall(
          abi.encodePacked(bytes1(0x86), uint128(id), uint16(index))
        );
        require(success, 'ADFS: call failed');

        return (bytes32(returnData));
      }
      ```
    </TabsContent>

</Tabs>

- To read historical data stored in a feed stored in more than 32 bytes:

<Tabs defaultValue="lib-call" className="mt-4">
  <TabsList>
    <TabsTrigger value="lib-call">Lib Call</TabsTrigger>
    <TabsTrigger value="raw-call">Raw Call</TabsTrigger>
  </TabsList>
    <TabsContent value="lib-call">
      ```solidity copy filename="solidity"
      function getDataAtIndex(
        uint256 id,
        uint256 index
      ) external view returns (bytes32[] memory) {
        return ADFS.getDataAtIndex(adfs, id, index);
      }
      ```
    </TabsContent>

      <TabsContent value="raw-call">
      ```solidity copy filename="solidity"
      function getDataAtIndex(
        uint256 id,
        uint256 index
      ) external view returns (bytes32[] memory data) {
        (bool success, bytes memory returnData) = adfs.staticcall(
          abi.encodePacked(bytes1(0x86), uint128(id), uint16(index))
        );
        require(success, 'ADFS: call failed');

        return parseBytesToBytes32Array(returnData);
      }
      ```
    </TabsContent>

</Tabs>

- To read a slice of the historical data stored in a feed stored in more than 32 bytes:

<Tabs defaultValue="lib-call" className="mt-4">
  <TabsList>
    <TabsTrigger value="lib-call">Lib Call</TabsTrigger>
    <TabsTrigger value="raw-call">Raw Call</TabsTrigger>
  </TabsList>
    <TabsContent value="lib-call">
      ```solidity copy filename="solidity"
      function getDataSliceAtIndex(
        uint256 id,
        uint256 index,
        uint256 startSlot,
        uint256 slotsCount
      ) external view returns (bytes32[] memory) {
        return ADFS.getDataSliceAtIndex(adfs, id, index, startSlot, slotsCount);
      }
      ```
    </TabsContent>

      <TabsContent value="raw-call">
      ```solidity copy filename="solidity"
      function getDataSliceAtIndex(
        uint256 id,
        uint256 index,
        uint32 startSlot,
        uint32 slotsCount
      ) external view returns (bytes32[] memory data) {
        (bool success, bytes memory returnData) = adfs.staticcall(
          abi.encodePacked(
            bytes1(0x86),
            uint128(id),
            uint16(index),
            startSlot,
            slotsCount
          )
        );
        require(success, 'ADFS: call failed');

        return parseBytesToBytes32Array(returnData);
      }
      ```
    </TabsContent>

</Tabs>

- To read the latest data stored in a feed stored in up to 32 bytes with its ring buffer index:

<Tabs defaultValue="lib-call" className="mt-4">
  <TabsList>
    <TabsTrigger value="lib-call">Lib Call</TabsTrigger>
    <TabsTrigger value="raw-call">Raw Call</TabsTrigger>
  </TabsList>
    <TabsContent value="lib-call">
      ```solidity copy filename="solidity"
      function getLatestSingleDataAndIndex(
        uint256 id
      ) external view returns (bytes32 data, uint256 index) {
        return ADFS.getLatestSingleDataAndIndex(adfs, id);
      }
      ```
    </TabsContent>

      <TabsContent value="raw-call">
      ```solidity copy filename="solidity"
      function getLatestSingleDataAndIndex(
        uint256 id
      ) external view returns (bytes32 data, uint256 index) {
        (bool success, bytes memory returnData) = adfs.staticcall(
          abi.encodePacked(bytes1(0x83), uint128(id))
        );
        require(success, 'ADFS: call failed');

        (index, data) = abi.decode(returnData, (uint256, bytes32));
      }
      ```
    </TabsContent>

</Tabs>

- To read the latest data stored in a feed stored in more than 32 bytes with its ring buffer index:

<Tabs defaultValue="lib-call" className="mt-4">
  <TabsList>
    <TabsTrigger value="lib-call">Lib Call</TabsTrigger>
    <TabsTrigger value="raw-call">Raw Call</TabsTrigger>
  </TabsList>
    <TabsContent value="lib-call">
      ```solidity copy filename="solidity"
      function getLatestDataAndIndex(
        uint256 id
      ) external view returns (bytes32[] memory data, uint256 index) {
        return ADFS.getLatestDataAndIndex(adfs, id);
      }
      ```
    </TabsContent>

      <TabsContent value="raw-call">
      ```solidity copy filename="solidity"
      function getLatestDataAndIndex(
        uint256 id
      ) external view returns (bytes32[] memory data, uint256 index) {
        (bool success, bytes memory returnData) = adfs.staticcall(
          abi.encodePacked(bytes1(0x85), uint128(id))
        );
        require(success, 'ADFS: call failed');

        index = uint256(bytes32(returnData));

        assembly {
          let len := mload(returnData)
          returnData := add(returnData, 32)
          mstore(returnData, sub(len, 32))
        }

        data = parseBytesToBytes32Array(returnData);
      }
      ```
    </TabsContent>

</Tabs>

- To read a slice of the latest data of a feed stored in more than 32 bytes with its ring buffer index:

<Tabs defaultValue="lib-call" className="mt-4">
  <TabsList>
    <TabsTrigger value="lib-call">Lib Call</TabsTrigger>
    <TabsTrigger value="raw-call">Raw Call</TabsTrigger>
  </TabsList>
    <TabsContent value="lib-call">
      ```solidity copy filename="solidity"
      function getLatestDataSliceAndIndex(
        uint256 id,
        uint256 startSlot,
        uint256 slotsCount
      ) external view returns (bytes32[] memory data, uint256 index) {
        return ADFS.getLatestDataSliceAndIndex(adfs, id, startSlot, slotsCount);
      }
      ```
    </TabsContent>

      <TabsContent value="raw-call">
      ```solidity copy filename="solidity"
      function getLatestDataSliceAndIndex(
        uint256 id,
        uint32 startSlot,
        uint32 slotsCount
      ) external view returns (bytes32[] memory data, uint256 index) {
        (bool success, bytes memory returnData) = adfs.staticcall(
          abi.encodePacked(bytes1(0x85), uint128(id), startSlot, slotsCount)
        );
        require(success, 'ADFS: call failed');

        index = uint256(bytes32(returnData));

        assembly {
          let len := mload(returnData)
          returnData := add(returnData, 32)
          mstore(returnData, sub(len, 32))
        }

        data = parseBytesToBytes32Array(returnData);
      }
      ```
    </TabsContent>

</Tabs>

### Solidity Hardhat Example

<Callout type="info" emoji="ðŸ’¡">
  You can find a working Hardhat project in the Blocksense repository. Clone the repo and follow the setup instructions to run the example locally. The example contract for using ADFS with the ADFS library can be found in [ADFSConsumer.sol](https://github.com/blocksense-network/blocksense/blob/main/libs/ts/contracts/contracts/test/examples/ADFSConsumer.sol) and for using ADFS via a raw call - in [RawCallADFSConsumer.sol](https://github.com/blocksense-network/blocksense/blob/main/libs/ts/contracts/contracts/test/examples/RawCallADFSConsumer.sol). A file with ethers utils can be found in [feedStoreConsumer.ts](https://github.com/blocksense-network/blocksense/blob/main/libs/ts/contracts/test/examples/utils/feedStoreConsumer.ts).
</Callout>

### Ethers.js v6.x

To read the latest data stored in a feed stored in up to 32 bytes:

```js copy filename="javascript"
// inputs: id

const adfs = new ethers.Contract(
  contractAddress,
  abiJson,
  provider,
);
const data = ethers.solidityPacked(['bytes1', 'uint128'], ['0x82', id]);

const res = await network.provider.send('eth_call', [
  {
    to: adfs.target,
    data,
  },
  'latest',
]);

const value = Number(res.slice(0, 50));
const timestamp = Number('0x' + res.slice(50, 66));
```

To read the latest data stored in a feed stored in more than 32 bytes:

```js copy filename="javascript"
// inputs: id

const adfs = new ethers.Contract(
  contractAddress,
  abiJson,
  provider,
);

const data = ethers.solidityPacked(['bytes1', 'uint128'], ['0x82', id]);

const res = await network.provider.send('eth_call', [
  {
    to: adfs.target,
    data,
  },
  'latest',
]);

const value = splitInto32bChunks(res);
```

To read a slice of the latest data stored in a feed stored in more than 32 bytes:

```js copy filename="javascript"
// inputs: id, startSlot, slots

const adfs = new ethers.Contract(
  contractAddress,
  abiJson,
  provider,
);

const data = ethers.solidityPacked(
  ['bytes1', 'uint128', 'uint32', 'uint32'],
  ['0x84', id, startSlot, slots],
);

const res = await network.provider.send('eth_call', [
  {
    to: adfs.target,
    data,
  },
  'latest',
]);

const value = splitInto32bChunks(res);
```

To get the latest ring buffer index:

```js copy filename="javascript"
// inputs: id

const adfs = new ethers.Contract(
  contractAddress,
  abiJson,
  provider,
);

const data = ethers.solidityPacked(['bytes1', 'uint128'], ['0x81', id]);

const res = await network.provider.send('eth_call', [
  {
    to: adfs.target,
    data,
  },
  'latest',
]);

const index = Number(res)
```

To read historical data stored in a feed stored in up to 32 bytes:

```js copy filename="javascript"
// inputs: id, indexId

const adfs = new ethers.Contract(
  contractAddress,
  abiJson,
  provider,
);

const data = ethers.solidityPacked(
  ['bytes1', 'uint128', 'uint16'],
  ['0x86', id, indexId],
);

const res = await network.provider.send('eth_call', [
  {
    to: adfs.target,
    data,
  },
  'latest',
]);

const value = Number(res.slice(0, 50));
const timestamp = Number('0x' + res.slice(50, 66));
```

To read historical data stored in a feed stored in more than 32 bytes:

```js copy filename="javascript"
// inputs: id, indexId

const adfs = new ethers.Contract(
  contractAddress,
  abiJson,
  provider,
);

const data = ethers.solidityPacked(
  ['bytes1', 'uint128', 'uint16'],
  ['0x86', id, indexId],
);

const res = await network.provider.send('eth_call', [
  {
    to: adfs.target,
    data,
  },
  'latest',
]);

const value = splitInto32bChunks(res);
```

To read a slice of the historical data stored in a feed stored in more than 32 bytes:

```js copy filename="javascript"
// inputs: id, indexId, startSlot, slots

const adfs = new ethers.Contract(
  contractAddress,
  abiJson,
  provider,
);

const data = ethers.solidityPacked(
  ['bytes1', 'uint128', 'uint16', 'uint32', 'uint32'],
  ['0x86', id, indexId, startSlot, slots],
);

const res = await network.provider.send('eth_call', [
  {
    to: adfs.target,
    data,
  },
  'latest',
]);

const value = splitInto32bChunks(res);
```

To read the latest data stored in a feed stored in up to 32 bytes with its ring buffer index:

```js copy filename="javascript"
// inputs: id

const adfs = new ethers.Contract(
  contractAddress,
  abiJson,
  provider,
);

const data = ethers.solidityPacked(['bytes1', 'uint128'], ['0x83', id]);

const res = await network.provider.send('eth_call', [
  {
    to: adfs.target,
    data,
  },
  'latest',
]);

const feedData = '0x' + res.slice(66);
const index = Number(res.slice(0, 66));
const value = Number(feedData.slice(0, 50));
const timestamp = Number('0x' + feedData.slice(50, 66));
```

To read the latest data stored in a feed stored in more than 32 bytes with its ring buffer index:

```js copy filename="javascript"
// inputs: id

const adfs = new ethers.Contract(
  contractAddress,
  abiJson,
  provider,
);

const data = ethers.solidityPacked(['bytes1', 'uint128'], ['0x85', id]);

const res = await network.provider.send('eth_call', [
  {
    to: adfs.target,
    data,
  },
  'latest',
]);

const value = splitInto32bChunks(res);
```

To read a slice of the latest data stored in a feed stored in more than 32 bytes with its ring buffer index:

```js copy filename="javascript"
// inputs: id, startSlot, slots

const adfs = new ethers.Contract(
  contractAddress,
  abiJson,
  provider,
);

const data = ethers.solidityPacked(
  ['bytes1', 'uint128', 'uint32', 'uint32'],
  ['0x85', id, startSlot, slots],
);

const res = await network.provider.send('eth_call', [
  {
    to: adfs.target,
    data,
  },
  'latest',
]);

const value = splitInto32bChunks(res);
```
